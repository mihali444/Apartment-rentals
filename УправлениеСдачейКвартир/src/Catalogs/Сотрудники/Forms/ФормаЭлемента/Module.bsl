#Область ОбработчикиСобытийФормы


&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЭтоАдресВременногоХранилища(АдресФото) Тогда
		ТекущийОбъект.Фото = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресФото));
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущийОбъект.ИндентификаторПользователяИБ) Тогда
		Если Не ВходРазрешен Тогда
			Возврат
		КонецЕсли;
		
		ПользовательИБ = ПользователиИнформационнойБазы.СоздатьПользователя();
	
	Иначе
		ПользовательИБ = 
			ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ТекущийОбъект.ИндентификаторПользователяИБ);
		
		Если ПользовательИБ = Неопределено Тогда
			ПользовательИБ = ПользователиИнформационнойБазы.СоздатьПользователя();
		КонецЕсли;
	
	КонецЕсли;
	
	ПользовательИБ.АутентификацияСтандартная = ВходРазрешен;
	ПользовательИБ.Имя = Логин;
	ПользовательИБ.ПолноеИмя = ТекущийОбъект.Наименование;
	
	Если ПарольИзменен Тогда
		ПользовательИБ.Пароль = Пароль;
	КонецЕсли;
	
	ПользовательИБ.Роли.Очистить();
	
	Если Объект.Должность = Перечисления.Должность.Администратор Тогда
		ПользовательИБ.Роли.Добавить(Метаданные.Роли.ПолныеПрава);
	ИначеЕсли Объект.Должность = Перечисления.Должность.МенеджерПоРаботеССобственниками Тогда
		ПользовательИБ.Роли.Добавить(Метаданные.Роли.МенеджерПоРаботеССобственниками);
	ИначеЕсли Объект.Должность = Перечисления.Должность.МенеджерПоРаботеСАрендаторами Тогда
		ПользовательИБ.Роли.Добавить(Метаданные.Роли.МенеджерПоРаботеСАрендаторами);
	КонецЕсли;
	
	ПользовательИБ.Записать();
	
	ТекущийОбъект.ИндентификаторПользователяИБ = ПользовательИБ.УникальныйИдентификатор;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	АдресФото = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "Фото");
	
	ПарольИзменен = Ложь;
	
	Если Не ЗначениеЗаполнено(ТекущийОбъект.ИндентификаторПользователяИБ) Тогда
		Возврат;
	КонецЕсли;
	
	ПользовательИБ = 
			ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ТекущийОбъект.ИндентификаторПользователяИБ);
	
	Если ПользовательИБ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Логин = ПользовательИБ.Имя;
	ВходРазрешен = ПользовательИБ.АутентификацияСтандартная;
	
	Если ПользовательИБ.ПарольУстановлен Тогда
		Пароль = Новый УникальныйИдентификатор;
	Иначе
		Пароль = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПарольИзменен = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЭтоАдресВременногоХранилища(АдресФото) Тогда
		ПоместитьВоВременноеХранилище(Неопределено, АдресФото);
		АдресФото = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "Фото");
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ПарольПриИзменении(Элемент)
	
	ПарольИзменен = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура АдресФотоНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыбратьФото();
	
КонецПроцедуры
#КонецОбласти
#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Очистить(Команда)
	
	Если ЭтоАдресВременногоХранилища(АдресФото) Тогда
		ПоместитьВоВременноеХранилище(Неопределено, АдресФото);
	КонецЕсли;
		
	АдресФото = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
КонецПроцедуры
#КонецОбласти



#Область СлужебныеПроцедурыИФункции
&НаКлиенте
Асинх Процедура ВыбратьФото()
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.Фильтр = НСтр("ru = 'Фото|*.jpg;*.png'");
		
	РезультатПомещения = Ждать ПоместитьФайлНаСерверАсинх( , , , ПараметрыДиалога, УникальныйИдентификатор);
	
	Если РезультатПомещения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатПомещения.ПомещениеФайлаОтменено Тогда
		Возврат;
	КонецЕсли;
	
	АдресФото = РезультатПомещения.Адрес;
	
КонецПроцедуры
#КонецОбласти