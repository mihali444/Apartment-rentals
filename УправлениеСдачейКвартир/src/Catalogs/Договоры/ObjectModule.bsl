#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	ДатаЗаключения = ТекущаяДатаСеанса();
	Ответственный = ПараметрыСеанса.ТекущийПользователь;

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Ответственный = ПараметрыСеанса.ТекущийПользователь;

КонецПроцедуры
Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОтправитьВМобильноеПриложение();

КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОтправитьВМобильноеПриложение()

	Если ВидДоговора <> Перечисления.ВидДоговора.САрендатором Тогда
		Возврат;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОбменСМобильнымПриложением.Ссылка КАК Ссылка
	|ИЗ
	|	ПланОбмена.ОбменСМобильнымПриложением КАК ОбменСМобильнымПриложением
	|ГДЕ
	|	НЕ ОбменСМобильнымПриложением.ПометкаУдаления
	|	И НЕ ОбменСМобильнымПриложением.ЭтотУзел";

	Выборка = Запрос.Выполнить().Выбрать();

	УзлыДляРегистрации = Новый Массив;
	Пока Выборка.Следующий() Цикл

		ОбменДанными.Получатели.Добавить(Выборка.Ссылка);
		УзлыДляРегистрации.Добавить(Выборка.Ссылка);

	КонецЦикла;

	Если ЗначениеЗаполнено(УзлыДляРегистрации) Тогда
		ПланыОбмена.ЗарегистрироватьИзменения(УзлыДляРегистрации, Владелец);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли