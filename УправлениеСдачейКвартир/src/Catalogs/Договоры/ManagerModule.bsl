
// Процедура заполняет табличный документ для печати.
//
// Параметры:
//	ТабДок - ТабличныйДокумент - табличный документ для заполнения и печати.
//	Ссылка - Произвольный - содержит ссылку на объект, для которого вызвана команда печати.
Процедура ПечатьДоговора(ТабДок, Ссылка) Экспорт
	
	Макет = ПолучитьМакет("ПечатьДоговора");
	
	// Получаем данные по договору
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Договоры.Код,
		|	Договоры.Наименование,
		|	Договоры.Квартира,
		|	Договоры.ВидДоговора,
		|	Договоры.Цена,
		|	Договоры.Ответственный,
		|	Договоры.ДатаНачала,
		|	Договоры.ДатаОкончания,
		|	Договоры.ДатаЗаключения,
		|   Договоры.Владелец.Наименование КАК Клиент
		|ИЗ
		|	Справочник.Договоры КАК Договоры
		|ГДЕ
		|	Договоры.Ссылка В (&Ссылка)";
	
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	Шапка = Макет.ПолучитьОбласть("Шапка"); 

	ТабДок.Очистить();
	
	ВставлятьРазделительСтраниц = Ложь;
	Пока Выборка.Следующий() Цикл
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПараметрыОбластиЗаголовок = Новый Структура;
		ПараметрыОбластиЗаголовок.Вставить("ДатаЗаключения", БезВремени(Выборка.ДатаЗаключения));
		ПараметрыОбластиЗаголовок.Вставить("Код", УдалитьЛидирующиеНули(Выборка.Код));
		
		ПараметрыОбластиШапка = Новый Структура;
		ПараметрыОбластиШапка.Вставить("Клиент", Выборка.Клиент);
		ПараметрыОбластиШапка.Вставить("Ответственный", Выборка.Ответственный);
		ПараметрыОбластиШапка.Вставить("ДатаНачала", БезВремени(Выборка.ДатаНачала));
		ПараметрыОбластиШапка.Вставить("ДатаОкончания", БезВремени(Выборка.ДатаОкончания));
		ПараметрыОбластиШапка.Вставить("Квартира", Выборка.Квартира);
		ПараметрыОбластиШапка.Вставить("Цена", Выборка.Цена);
		ПараметрыОбластиШапка.Вставить("ЦенаПрописью", ЧислоПрописью(
														Выборка.Цена,
														"Л=ru_RU;НП=Истина;НД=Истина;ДП=Ложь",
														"рубль,рубля,рублей,м,копейка,копейки,копеек"));
		
		

		ОбластьЗаголовок.Параметры.Заполнить(ПараметрыОбластиЗаголовок);
		Шапка.Параметры.Заполнить(ПараметрыОбластиШапка);
	
		ТабДок.Вывести(ОбластьЗаголовок);
		ТабДок.Вывести(Шапка);
	КонецЦикла;

КонецПроцедуры

#Область СлужебныеПроцедурыИФункции

Функция УдалитьЛидирующиеНули(Код)
		
	Результат = Код;
		
	Пока СтрНачинаетсяС(Результат, "0") Цикл	
		Результат = Сред(Результат, 2);	
	КонецЦикла;
	 
	Возврат Результат;
	
КонецФункции

Функция БезВремени(Дата)
		Если Дата = Неопределено Тогда
			Возврат "";
		Иначе
			Возврат Формат(Дата, "ДЛФ=D;");
		КонецЕсли;
КонецФункции

#КонецОбласти