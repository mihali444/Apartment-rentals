
#Область ПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СформироватьДанныеОтвета(ИдентификаторКлиента, НомерПринятогоСообщения) Экспорт
	
	Попытка
		Возврат ПодготовитьОтвет(ИдентификаторКлиента, НомерПринятогоСообщения);	
	Исключение
		ЗаписьЖурналаРегистрации("ОбменСМобильным.Ошибка", УровеньЖурналаРегистрации.Ошибка,,,
		ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
	КонецПопытки;
	
КонецФункции

Функция ПодготовитьОтвет(ИдентификаторКлиента, НомерПринятогоСообщения)
	
	Узел = ПланыОбмена.ОбменСМобильнымПриложением.НайтиПоКоду(ИдентификаторКлиента);
	
	Если Не ЗначениеЗаполнено(Узел) Тогда
		УзелОбъект = ПланыОбмена.ОбменСМобильнымПриложением.СоздатьУзел();
		УзелОбъект.Код = ИдентификаторКлиента;
		УзелОбъект.Пользователь = ПараметрыСеанса.ТекущийПользователь;
		УзелОбъект.Наименование = УзелОбъект.Пользователь;
		УзелОбъект.Записать();
		ПервичнаяРегистрация(УзелОбъект.Ссылка);
		Узел = УзелОбъект.Ссылка;
	КонецЕсли;
    
    // ДОБАВЬТЕ ПРОВЕРКУ номера сообщения
    Если НомерПринятогоСообщения < 0 Тогда
        НомерПринятогоСообщения = 0;
    КонецЕсли;
	
	Попытка
        ПланыОбмена.УдалитьРегистрациюИзменений(Узел, НомерПринятогоСообщения);
    Исключение
        // Логируем ошибку, но продолжаем выполнение
        ЗаписьЖурналаРегистрации("ОбменСМобильным.ОшибкаУдаленияРегистрации", 
            УровеньЖурналаРегистрации.Ошибка,,,
            "Ошибка при удалении регистрации изменений: " + 
            ОписаниеОшибки());
    КонецПопытки;
	
	НомерСообщения = НомерПринятогоСообщения + 1;
	
	ВыборкаИзменений = ПланыОбмена.ВыбратьИзменения(Узел, НомерСообщения);
	
	ИзмененныеОбъекты = Новый Массив;
	
	Пока ВыборкаИзменений.Следующий() Цикл
		
		Объект = ВыборкаИзменений.Получить();
		
		Если ТипЗнч(Объект) = Тип("СправочникОбъект.Квартиры") Тогда
			ОписаниеОбъекта = ОписаниеКвартиры(Объект);
		ИначеЕсли ТипЗнч(Объект) = Тип("СправочникОбъект.Договоры") Тогда
			ОписаниеОбъекта = ОписаниеДоговора(Объект);
		ИначеЕсли ТипЗнч(Объект) = Тип("СправочникОбъект.Клиенты") Тогда
			ОписаниеОбъекта = ОписаниеКлиента(Объект);
		ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.ЗаданияДляГоничной") Тогда
			ОписаниеОбъекта = ОписаниеЗадания(Объект);
		Иначе
			Продолжить;
		КонецЕсли;
		
		
		ИзмененныеОбъекты.Добавить(ОписаниеОбъекта);
		
		
	КонецЦикла;
	
	ДанныеДляОтправки = Новый Структура;
	ДанныеДляОтправки.Вставить("ИзмененныеОбъекты", ИзмененныеОбъекты);
	ДанныеДляОтправки.Вставить("НомерСообщения", НомерСообщения);
	
	Возврат ЗаписатьЗначениеJSON(ДанныеДляОтправки);
	
КонецФункции

Функция ОписаниеЗадания(Объект)
	Результат = Новый Структура;
	Результат.Вставить("Тип", "Документ.ЗаданияДляГорничной");
	Результат.Вставить("Дата", Формат(Объект.Дата, "ДФ=yyyyMMddHHmmss"));
	Результат.Вставить("Идентификатор", Строка(Объект.Ссылка.УникальныйИдентификатор()));
	Результат.Вставить("ДатаУборки", Формат(Объект.ДатаУборки, "ДФ=yyyyMMddHHmmss"));
	Результат.Вставить("ЗаданиеНаУборку", Объект.ЗаданиеНаУборку);
	Результат.Вставить("Квартира", Строка(Объект.Квартира.УникальныйИдентификатор()));
	
	Возврат Результат;
КонецФункции

Функция ОписаниеДоговора(Объект)
	
	Результат = Новый Структура;
	Результат.Вставить("Тип", "Справочник.Договоры");
	Результат.Вставить("Идентификатор", Строка(Объект.Ссылка.УникальныйИдентификатор()));
	Результат.Вставить("Наименование", Объект.Наименование);
	Результат.Вставить("ДатаНачала", Формат(Объект.ДатаНачала, "ДФ=yyyyMMddHHmmss"));
	Результат.Вставить("ДатаОкончания", Формат(Объект.ДатаОкончания, "ДФ=yyyyMMddHHmmss"));
	Результат.Вставить("Квартира", Строка(Объект.Квартира.УникальныйИдентификатор()));
	Результат.Вставить("Владелец", Строка(Объект.Владелец.УникальныйИдентификатор()));
	
	Возврат Результат;
	
КонецФункции

Функция ОписаниеКлиента(Объект)
	
	Результат = Новый Структура;
	Результат.Вставить("Тип", "Справочник.Клиенты");
	Результат.Вставить("Идентификатор", Строка(Объект.Ссылка.УникальныйИдентификатор()));
	Результат.Вставить("Наименование", Объект.Наименование);
	Результат.Вставить("Телефон", Объект.Телефон);
	
	Возврат Результат;
	
КонецФункции

Функция ОписаниеКвартиры(Объект)
	
	Результат = Новый Структура;
	Результат.Вставить("Тип", "Справочник.Квартиры");
	Результат.Вставить("Идентификатор", Строка(Объект.Ссылка.УникальныйИдентификатор()));
	Результат.Вставить("Наименование", Объект.Наименование);
	Результат.Вставить("КоличествоКомнат", Объект.КоличествоКомнат);
	
	Возврат Результат;	
	
КонецФункции

Процедура ПервичнаяРегистрация(Узел)
	
	ОбъектыДляРегистрации = Новый Массив;

	Запрос = Новый Запрос;
	Запрос.Текст = 
			"ВЫБРАТЬ
		|	Квартиры.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Квартиры КАК Квартиры
		|ГДЕ
		|	Квартиры.ЭтоГруппа = ЛОЖЬ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ОбъектыДляРегистрации.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Договоры.Владелец КАК Клиент,
		|	Договоры.Ссылка КАК Договор
		|ИЗ
		|	Справочник.Договоры КАК Договоры
		|ГДЕ
		|	Договоры.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидДоговора.САрендатором)
		|ИТОГИ
		|ПО
		|	Владелец";
	
	ВыборкаКлиенты = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаКлиенты.Следующий() Цикл
		ОбъектыДляРегистрации.Добавить(ВыборкаКлиенты.Клиент);
		ВыборкаДоговоры = ВыборкаКлиенты.Выбрать();
		Пока ВыборкаДоговоры.Следующий() Цикл
			ОбъектыДляРегистрации.Добавить(ВыборкаДоговоры.Договор);
		КонецЦикла;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаданияДляГоничной.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗаданияДляГоничной КАК ЗаданияДляГоничной";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбъектыДляРегистрации.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Если ОбъектыДляРегистрации.Количество() > 0 Тогда
		ПланыОбмена.ЗарегистрироватьИзменения(Узел, ОбъектыДляРегистрации);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
