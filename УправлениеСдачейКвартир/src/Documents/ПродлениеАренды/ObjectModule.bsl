#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.СдачаВАренду") Тогда
		// Заполнение шапки
		Квартира = ДанныеЗаполнения.Квартира;
		ДатаНачала = ДанныеЗаполнения.ДатаОкончания;
		Клиент = ДанныеЗаполнения.Клиент;
		Сумма = ДанныеЗаполнения.Сумма;
		Договор = ДанныеЗаполнения.Договор;
		Ответственный = ДанныеЗаполнения.Ответственный;

	КонецЕсли;

	Ответственный = ПараметрыСеанса.ТекущийПользователь;

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	СформироватьДвиженияПродажи(Отказ);
	СформироватьДвиженияЗадолженностьАрендаторов(Отказ);
	СформироватьДвиженияКонтрольСдачиКвартир(Отказ);

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Ответственный = ПараметрыСеанса.ТекущийПользователь;

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
		
	ПроверкиВДокументах.ПроверитьДоговор(Отказ, Договор, ДатаНачала, ДатаОкончания, ЭтотОбъект);
	ПроверкиВДокументах.ПроверкаКвартиры(Отказ, Квартира, Договор.Квартира, ЭтотОбъект);
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Процедура СформироватьДвиженияКонтрольСдачиКвартир(Отказ)
	// регистр КонтрольСдачиКвартир
	
	Движения.КонтрольСдачиКвартир.Записывать = Истина;
	Движение = Движения.КонтрольСдачиКвартир.Добавить();
	Движение.ДатаНачала = ДатаНачала;
	Движение.ДатаОкончания = ДатаОкончания;
	Движение.Договор = Договор;
	Движение.Квартира = Квартира;
	Движение.Сумма = Сумма;
	
КонецПроцедуры

Процедура СформироватьДвиженияПродажи(Отказ)
	// регистр Продажи
	Движения.Продажи.Записывать = Истина;
	Движение = Движения.Продажи.Добавить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Покупки.Квартира КАК Квартира,
		|	Покупки.Договор.Цена КАК ДоговорЦена
		|ИЗ
		|	РегистрНакопления.Покупки КАК Покупки
		|ГДЕ
		|	Покупки.Квартира = &Квартира
		|
		|УПОРЯДОЧИТЬ ПО
		|	Покупки.МоментВремени УБЫВ";
	
	Запрос.УстановитьПараметр("Квартира", Квартира);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Движение.Себестоимость = ВыборкаДетальныеЗаписи.ДоговорЦена * КоличествоДней / 30 ;
	КонецЕсли;
	
	Движение.Период = Дата;
	Движение.Клиент = Клиент;
	Движение.Квартира = Квартира;
	Движение.Договор = Договор;
	Движение.Сумма = Сумма;
КонецПроцедуры

Процедура СформироватьДвиженияЗадолженностьАрендаторов(Отказ)
	
	Движения.ЗадолженностьАрендаторов.Записывать = Истина;
	Движения.АвансыАрендаторов.Записывать = Истина;
	Движения.Записать();
	
	Блокировка = Новый БлокировкаДанных;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ЗадолженностьАрендаторов");
	ЭлементБлокировки.УстановитьЗначение("Клиент", Клиент);
	ЭлементБлокировки.УстановитьЗначение("Договор", Договор);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.АвансыАрендаторов");
	ЭлементБлокировки.УстановитьЗначение("Клиент", Клиент);
	ЭлементБлокировки.УстановитьЗначение("Договор", Договор);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	Блокировка.Заблокировать();
	
	Движения.ЗадолженностьАрендаторов.Записывать = Истина;
	Движения.АвансыАрендаторов.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АвансыАрендаторовОстатки.СуммаОстаток КАК Аванс
		|ИЗ
		|	РегистрНакопления.АвансыАрендаторов.Остатки(
		|			&Дата,
		|			Клиент = &Клиент
		|				И Договор = &Договор) КАК АвансыАрендаторовОстатки";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Договор", Договор);
	Запрос.УстановитьПараметр("Клиент", Клиент);
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Аванс = 0;
	
	Если РезультатЗапроса.Следующий() Тогда
		Аванс = РезультатЗапроса.Аванс;
	КонецЕсли;
	
	Если Аванс > 0 Тогда
		
		СуммаАванса = Мин(Аванс, Сумма);
		
		ДвижениеАванс = Движения.АвансыАрендаторов.Добавить();
		ДвижениеАванс.Период = Дата;
		ДвижениеАванс.ВидДвижения = ВидДвиженияНакопления.Расход;
		ДвижениеАванс.Клиент = Клиент;
		ДвижениеАванс.Договор = Договор;
		ДвижениеАванс.Сумма = СуммаАванса;
		
		Если Сумма > Аванс Тогда
			Движение = Движения.ЗадолженностьАрендаторов.Добавить();
			Движение.Период = Дата;
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Клиент = Клиент;
			Движение.Договор = Договор;
			Движение.Сумма = Сумма - Аванс;
		КонецЕсли;
		
	Иначе
		Движение = Движения.ЗадолженностьАрендаторов.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Клиент = Клиент;
		Движение.Договор = Договор;
		Движение.Сумма = Сумма;
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти

#КонецЕсли