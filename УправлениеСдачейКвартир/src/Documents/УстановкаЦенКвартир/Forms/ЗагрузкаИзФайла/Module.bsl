
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбработатьФайлЦен(Параметры.АдресФайла);
	
	ПодобратьКвартиру();

КонецПроцедуры
#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

// Код процедур и функций

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	Адрес = ПоместитьТаблицу();
	
	Закрыть(Адрес);
	
КонецПроцедуры
#КонецОбласти


#Область СлужебныеПроцедурыИФункции
&НаСервере
Функция ПоместитьТаблицу()
	
	Возврат ПоместитьВоВременноеХранилище(ДанныеФайла.Выгрузить());
	
КонецФункции

&НаСервере
Процедура ПодобратьКвартиру()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Т.Наименование КАК Наименование,
		|	Т.Цена КАК Цена
		|ПОМЕСТИТЬ ВТ_ДанныеФайла
		|ИЗ
		|	&ДанныеФайла КАК Т
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДанныеФайла.Наименование КАК Наименование,
		|	ВТ_ДанныеФайла.Цена КАК Цена,
		|	МАКСИМУМ(Квартиры.Ссылка) КАК Квартира
		|ИЗ
		|	ВТ_ДанныеФайла КАК ВТ_ДанныеФайла
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Квартиры КАК Квартиры
		|		ПО ВТ_ДанныеФайла.Наименование = Квартиры.Наименование
		|		И НЕ Квартиры.ПометкаУдаления
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ДанныеФайла.Наименование,
		|	ВТ_ДанныеФайла.Цена";
		
		
	Запрос.УстановитьПараметр("ДанныеФайла", ДанныеФайла.Выгрузить());
	
	ДанныеФайла.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьФайлЦен(Адрес)

	ДвоичныеДанные = ПолучитьИзВременногоХранилища(Адрес);
	ИмяФайла = ПолучитьИмяВременногоФайла("xlsx");

	ДвоичныеДанные.Записать(ИмяФайла);

	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.Прочитать(ИмяФайла);

	Для НомерСтроки = 2 По ТабличныйДокумент.ВысотаТаблицы Цикл

		Наименование = ТабличныйДокумент.Область(НомерСтроки, 1).Текст;
		Цена = ТабличныйДокумент.Область(НомерСтроки, 2).Текст;

		Строка = ДанныеФайла.Добавить();
		Строка.Наименование = Наименование;
		Строка.Цена = Число(Цена);
	КонецЦикла;

	УдалитьФайлы(ИмяФайла);

КонецПроцедуры

#КонецОбласти
