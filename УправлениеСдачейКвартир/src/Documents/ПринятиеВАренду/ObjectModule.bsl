#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ,Режим)
	
	СформироватьДвиженияПокупки(Отказ);
	СформироватьДвиженияЗадолженностьПередАрендодателями(Отказ);

КонецПроцедуры



Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	 ПроверкиВДокументах.ПроверитьДоговор(Отказ, Договор, ДатаНачала, ДатаОкончания, ЭтотОбъект);
	 ПроверкиВДокументах.ПроверкаКвартиры(Отказ, Квартира, Договор.Квартира, ЭтотОбъект);
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Процедура СформироватьДвиженияПокупки(Отказ)
	
	// регистр Покупки
	Движения.Покупки.Записывать = Истина;
	Движение = Движения.Покупки.Добавить();
	Движение.Период = Дата;
	Движение.Клиент = Клиент;
	Движение.Квартира = Квартира;
	Движение.Договор = Договор;
	Движение.Сумма = Сумма;
	
КонецПроцедуры

Процедура СформироватьДвиженияЗадолженностьПередАрендодателями(Отказ)
	
	Движения.ЗадолженностьПередАрендодателями.Записывать = Истина;
	Движения.АвансыАрендодателям.Записывать = Истина;
	Движения.Записать();
	
	Блокировка = Новый БлокировкаДанных;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ЗадолженностьПередАрендодателями");
	ЭлементБлокировки.УстановитьЗначение("Клиент", Клиент);
	ЭлементБлокировки.УстановитьЗначение("Договор", Договор);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.АвансыАрендодателям");
	ЭлементБлокировки.УстановитьЗначение("Клиент", Клиент);
	ЭлементБлокировки.УстановитьЗначение("Договор", Договор);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	Блокировка.Заблокировать();
	
	Движения.ЗадолженностьПередАрендодателями.Записывать = Истина;
	Движения.АвансыАрендодателям.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АвансыАрендодателямОстатки.СуммаОстаток КАК Аванс
		|ИЗ
		|	РегистрНакопления.АвансыАрендодателям.Остатки(
		|			&Дата,
		|			Клиент = &Клиент
		|				И Договор = &Договор) КАК АвансыАрендодателямОстатки";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Договор", Договор);
	Запрос.УстановитьПараметр("Клиент", Клиент);
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Аванс = 0;
	
	Если РезультатЗапроса.Следующий() Тогда
		Аванс = РезультатЗапроса.Аванс;
	КонецЕсли;
	
	Если Аванс > 0 Тогда
		
		СуммаАванса = Мин(Аванс, Сумма);
		
		ДвижениеАванс = Движения.АвансыАрендодателям.Добавить();
		ДвижениеАванс.Период = Дата;
		ДвижениеАванс.ВидДвижения = ВидДвиженияНакопления.Расход;
		ДвижениеАванс.Клиент = Клиент;
		ДвижениеАванс.Договор = Договор;
		ДвижениеАванс.Сумма = СуммаАванса;
		
		Если Сумма > Аванс Тогда
			Движение = Движения.ЗадолженностьПередАрендодателями.Добавить();
			Движение.Период = Дата;
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Клиент = Клиент;
			Движение.Договор = Договор;
			Движение.Сумма = Сумма - Аванс;
		КонецЕсли;
		
	Иначе
		Движение = Движения.ЗадолженностьПередАрендодателями.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Клиент = Клиент;
		Движение.Договор = Договор;
		Движение.Сумма = Сумма - Аванс;
	КонецЕсли;
	
КонецПроцедуры



#КонецОбласти

#Область Инициализация

#КонецОбласти

#КонецЕсли
