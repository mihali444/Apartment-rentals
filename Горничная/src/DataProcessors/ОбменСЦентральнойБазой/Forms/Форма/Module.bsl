
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Адрес = Константы.АдресЦентральнойБазы.Получить();
	Логин = Константы.ЛогинЦентральнойБазы.Получить();
	Пароль = Константы.ПарольЦентральнойБазы.Получить();
	
КонецПроцедуры
#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура АдресПриИзменении(Элемент)
	СохранитьНастройки();
КонецПроцедуры

&НаКлиенте
Процедура ЛогинПриИзменении(Элемент)
	СохранитьНастройки();
КонецПроцедуры

&НаКлиенте
Процедура ПарольПриИзменении(Элемент)
	СохранитьНастройки();
КонецПроцедуры
#КонецОбласти


#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ВыполнитьОбмен(Команда)
	ВыполнитьОбменНаСервере();
КонецПроцедуры
#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьНастройки()
	
	Константы.АдресЦентральнойБазы.Установить(Адрес);
	Константы.ЛогинЦентральнойБазы.Установить(Логин);
	Константы.ПарольЦентральнойБазы.Установить(Пароль);
	
КонецПроцедуры


&НаСервере
Процедура ВыполнитьОбменНаСервере()
	
	НастройкиПодключения = НастройкиПодключения();
	
	Соединение = Новый HTTPСоединение(НастройкиПодключения.АдресСервера, , 
		НастройкиПодключения.Логин, 
		НастройкиПодключения.Пароль, , , 
		НастройкиПодключения.ЗащищенноеСоединение);
		
	СистемнаяИнформация = Новый СистемнаяИнформация;
	ИдентификаторКлиента = Строка(СистемнаяИнформация.ИдентификаторКлиента);
	
	ЭтотУзел = ПланыОбмена.ОбменСЦентральнойБазой.ЭтотУзел();
	НомерПринятого = НомерПринятогоСообщения(ЭтотУзел);
		
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("X-MobileID", ИдентификаторКлиента);
	Заголовки.Вставить("X-Received-Message", НомерПринятого);
	
	АдресРесурса = НастройкиПодключения.ПутьНаСервере + "/hs/mobile/exchange";
	Запрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	
	//@skip-check bsl-legacy-check-dynamic-feature-access - только на клиенте
	Ответ = Соединение.Получить(Запрос);
	
	Если Ответ.КодСостояния <> 200 Тогда
		ВызватьИсключение СтрШаблон("Ошибка при получении данных от сервера: %1", Ответ.КодСостояния);
	КонецЕсли;

	ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
	ДанныеОтвета = ПрочитатьЗначениеJSON(ТелоОтвета);
	
	НомерПринятого = ДанныеОтвета.НомерСообщения;

	Для Каждого ОписаниеОбъекта Из ДанныеОтвета.ИзмененныеОбъекты Цикл
		Если ОписаниеОбъекта.Тип = "Справочник.Договоры" Тогда
			ЗаписатьДоговор(ОписаниеОбъекта);
		ИначеЕсли ОписаниеОбъекта.Тип = "Документ.ЗаданияДляГорничной" Тогда
			ЗаписатьДокументЗадания(ОписаниеОбъекта);
		ИначеЕсли ОписаниеОбъекта.Тип = "Справочник.Клиенты" Тогда
			ЗаписатьКлиента(ОписаниеОбъекта);
		ИначеЕсли ОписаниеОбъекта.Тип = "Справочник.Квартиры" Тогда
			ЗаписатьКвартиру(ОписаниеОбъекта);
		Иначе
			Продолжить;
		КонецЕсли;
	КонецЦикла;
	
	УзелОбъект = ЭтотУзел.ПолучитьОбъект();
	УзелОбъект.ОбменДанными.Загрузка = Истина;
	УзелОбъект.ЭтотУзел = Ложь;
	УзелОбъект.НомерПринятого = НомерПринятого;
	УзелОбъект.ЭтотУзел = Истина;
	УзелОбъект.Записать();
	
КонецПроцедуры

&НаСервере
Функция НомерПринятогоСообщения(Узел)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОбменСЦентральнойБазой.НомерПринятого КАК НомерПринятого
		|ИЗ
		|	ПланОбмена.ОбменСЦентральнойБазой КАК ОбменСЦентральнойБазой
		|ГДЕ
		|	ОбменСЦентральнойБазой.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Узел);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.НомерПринятого;
	
КонецФункции

&НаСервере
Процедура ЗаписатьДокументЗадания(ОписаниеОбъекта)
	Ссылка = Документы.ЗаданияНаУборку.ПолучитьСсылку(Новый УникальныйИдентификатор(ОписаниеОбъекта.Идентификатор));
	
	ДокОбъект = Ссылка.ПолучитьОбъект();
	Если ДокОбъект = Неопределено Тогда
		ДокОбъект = Документы.ЗаданияНаУборку.СоздатьДокумент();
		ДокОбъект.УстановитьСсылкуНового(Ссылка);
	КонецЕсли;
	
	ДокОбъект.Дата = ОписаниеОбъекта.Дата;
	ДокОбъект.ДатаУборки = ОписаниеОбъекта.ДатаУборки;
	ДокОбъект.Квартира = Справочники.Квартиры.ПолучитьСсылку(Новый УникальныйИдентификатор(ОписаниеОбъекта.Квартира));
	ДокОбъект.ЗаданиеНаУборку = ОписаниеОбъекта.ЗаданиеНаУборку;
	
	ДокОбъект.ОбменДанными.Загрузка = Истина;
	
	ДокОбъект.Записать();
КонецПроцедуры

&НаСервере
Процедура ЗаписатьКвартиру(ОписаниеОбъекта)
	Ссылка = Справочники.Квартиры.ПолучитьСсылку(Новый УникальныйИдентификатор(ОписаниеОбъекта.Идентификатор));
	
	СпрОбъект = Ссылка.ПолучитьОбъект();
	Если СпрОбъект = Неопределено Тогда
		СпрОбъект = Справочники.Квартиры.СоздатьЭлемент();
		СпрОбъект.УстановитьСсылкуНового(Ссылка);
	КонецЕсли;
	
	СпрОбъект.Наименование = ОписаниеОбъекта.Наименование;
	СпрОбъект.КоличествоКомнат = ОписаниеОбъекта.КоличествоКомнат;
	
	СпрОбъект.ОбменДанными.Загрузка = Истина;
	
	СпрОбъект.Записать();
КонецПроцедуры

&НаСервере
Процедура ЗаписатьКлиента(ОписаниеОбъекта)
	Ссылка = Справочники.Клиенты.ПолучитьСсылку(Новый УникальныйИдентификатор(ОписаниеОбъекта.Идентификатор));
	
	СпрОбъект = Ссылка.ПолучитьОбъект();
	Если СпрОбъект = Неопределено Тогда
		СпрОбъект = Справочники.Клиенты.СоздатьЭлемент();
		СпрОбъект.УстановитьСсылкуНового(Ссылка);
	КонецЕсли;
	
	СпрОбъект.Наименование = ОписаниеОбъекта.Наименование;
	СпрОбъект.Телефон = ОписаниеОбъекта.Телефон;
	
	СпрОбъект.ОбменДанными.Загрузка = Истина;
	
	СпрОбъект.Записать();
КонецПроцедуры

&НаСервере
Процедура ЗаписатьДоговор(ОписаниеОбъекта)
	
	Ссылка = Справочники.Договоры.ПолучитьСсылку(Новый УникальныйИдентификатор(ОписаниеОбъекта.Идентификатор));
	
	СпрОбъект = Ссылка.ПолучитьОбъект();
	Если СпрОбъект = Неопределено Тогда
		СпрОбъект = Справочники.Договоры.СоздатьЭлемент();
		СпрОбъект.УстановитьСсылкуНового(Ссылка);
	КонецЕсли;
	
	СпрОбъект.Наименование = ОписаниеОбъекта.Наименование;
	СпрОбъект.ДатаНачала = ОписаниеОбъекта.ДатаНачала;
	СпрОбъект.ДатаОкончания = ОписаниеОбъекта.ДатаОкончания;
	СпрОбъект.Клиент = Справочники.Клиенты.ПолучитьСсылку(Новый УникальныйИдентификатор(ОписаниеОбъекта.Владелец));
	СпрОбъект.Квартира = ОписаниеОбъекта.Квартира;
	
	СпрОбъект.ОбменДанными.Загрузка = Истина;
	
	СпрОбъект.Записать();
	
КонецПроцедуры

&НаСервере
Функция НастройкиПодключения()
	
	Результат = Новый Структура;
	Результат.Вставить("Логин", Логин);
	Результат.Вставить("Пароль", Пароль);
	
	Если СтрНачинаетсяС(Адрес, "https") Тогда
		ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL();
	Иначе
		ЗащищенноеСоединение = Неопределено;
	КонецЕсли;
		
	Результат.Вставить("ЗащищенноеСоединение", ЗащищенноеСоединение);
	
	Курсор = СтрНайти(Адрес, "://");
	АдресБезПротокола = Сред(Адрес, Курсор + 3);

	Курсор = СтрНайти(АдресБезПротокола, "/");
	АдресСервера = Лев(АдресБезПротокола, Курсор - 1);
	ПутьНаСервере = Сред(АдресБезПротокола, Курсор);
	
	Результат.Вставить("АдресСервера", АдресСервера);
	Результат.Вставить("ПутьНаСервере", ПутьНаСервере);
	
	Возврат Результат;
	
КонецФункции
#КонецОбласти
